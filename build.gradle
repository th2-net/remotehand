plugins {
    id "java"
    id 'application'
    id 'maven-publish'
    id "io.github.gradle-nexus.publish-plugin" version "1.0.0"
    id 'signing'
}

group = 'com.exactpro.remotehand'
version = release_version

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

ext {
    sharedDir = file("${project.rootDir}/shared")
}

application {
    mainClassName = "com.exactpro.remotehand.RemoteHandStarter"
}

repositories {
    mavenCentral()
    maven {
            name 'Sonatype_snapshots'
            url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
            name 'Sonatype_releases'
            url 'https://s01.oss.sonatype.org/content/repositories/releases/'
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

dependencies {
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.4.2'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.6.4'
    compile group: 'commons-io', name: 'commons-io', version: '2.4'
    compile group: 'net.sourceforge.javacsv', name: 'javacsv', version: '2.0'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.7'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.2'
    compile group: 'com.jhlabs', name: 'filters', version: '2.0.235'
    compile group: 'io.netty', name: 'netty-all', version: '4.1.33.Final'
    compile(group: 'org.seleniumhq.selenium', name: 'selenium-server', version: '3.141.59') {
        exclude group: 'org.seleniumhq.selenium', module: 'selenium-opera-driver'
        exclude group: 'org.seleniumhq.selenium', module: 'selenium-safari-driver'
    }
    
    compile 'io.appium:java-client:7.3.0'
    compile "org.mvel:mvel2:2.4.7.Final"
    compile 'net.java.dev.jna:jna:5.7.0'
}

jar {
    archivesBaseName = "${archivesBaseName}"
    manifest {
        attributes 'Main-Class': 'com.exactpro.remotehand.RemoteHandStarter'
        attributes "Class-Path": configurations.runtimeClasspath.collect { "lib/${it.getName()}" }.join(' ')
        attributes('Created-By': System.getProperty('java.version') + ' (' + System.getProperty('java.vendor') + ')')
        attributes('Specification-Title': 'Remotehand')
        attributes('Specification-Vendor': 'Exactpro Systems LLC')
        attributes('Implementation-Title': rootProject.name)
        attributes('Implementation-Vendor': 'Exactpro Systems LLC')
        attributes('Implementation-Vendor-Id': 'com.exactpro')
        attributes('Implementation-Version': project.version)
    }
}

task releaseZip (type: Zip, dependsOn: [jar, configurations.runtimeClasspath]) {
    archiveAppendix = new Date().format('yyyyMMdd')
    archiveVersion = release_version
    from jar
    from ("${projectDir}") {
        include "config.ini"
        include "log4j.properties"
        include "README.md"
    }
    into ("lib/") {
        from configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { it }
    }
}

// conditionals for publications
tasks.withType(PublishToMavenRepository) {
    onlyIf {
        (repository == publishing.repositories.nexusRepository &&
            project.hasProperty('nexus_user') &&
            project.hasProperty('nexus_password') &&
            project.hasProperty('nexus_url')) ||
        (repository == publishing.repositories.sonatype &&
            project.hasProperty('sonatypeUsername') &&
            project.hasProperty('sonatypePassword')) ||
        (repository == publishing.repositories.localRepo)
    }
}
tasks.withType(Sign) {
    onlyIf { project.hasProperty('signingKey') &&
        project.hasProperty('signingPassword')
    }
}
// disable running task 'initializeSonatypeStagingRepository' on a gitlab
tasks.whenTaskAdded {task ->
    if(task.name.equals('initializeSonatypeStagingRepository') &&
        !(project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword'))
    ) {
        task.enabled = false
    }
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom {
              name = rootProject.name
              packaging = 'jar'
              description = rootProject.description
              url = vcs_url
              scm {
                url = vcs_url
              }
              licenses {
                license {
                  name = 'The Apache License, Version 2.0'
                  url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
              }
              developers {
                developer {
                  id = 'developer'
                  name = 'developer'
                  email = 'developer@exactpro.com'
                }
              }
              scm {
                url = vcs_url
              }
            }
        }
    }
    repositories {
        maven {
            name = 'localRepo'
            url = sharedDir
        }
//Nexus repo to publish from gitlab
        maven {
            name = 'nexusRepository'
            credentials {
                username = project.findProperty('nexus_user')
                password = project.findProperty('nexus_password')
            }
            url = project.findProperty('nexus_url')
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

clean {
    delete sharedDir
}

 wrapper {
     gradleVersion = '6.6'
 }